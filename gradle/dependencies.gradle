ext {
    libraries = [:]
}

libraries += [
        'maven-shared-utils': 'org.apache.maven.shared:maven-shared-utils:0.3',
        groovy: 'org.codehaus.groovy:groovy:2.1.7:indy',
        junit: 'junit:junit:4.11',
        asm: 'org.ow2.asm:asm:4.1',
        hamcrest: 'org.hamcrest:hamcrest-library:1.3',
        jcommander: 'com.beust:jcommander:1.30',
]

libraries.dropwizard = ['com.yammer.dropwizard:dropwizard-core:0.6.2',
        'com.sun.jersey:jersey-client:1.17.1',
]

libraries.spock = ['org.spockframework:spock-core:0.7-groovy-2.0',
        libraries.groovy,
        'org.objenesis:objenesis:2.0',
        'cglib:cglib-nodep:2.2'
]

allprojects {
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            def req = details.requested
            // Replace groovy-all with groovy
            if (req.name == 'groovy-all' || req.name == 'groovy') {
                details.useTarget libraries.groovy
            }
            // Replace junit-dep with junit
            if (req.name == 'junit-dep') {
                details.useTarget libraries.junit
            }
            // Change old groupId of asm to new groupId, for correct dependency handling
            if (req.group == 'asm') {
                details.useTarget group: 'org.ow2.asm', name: req.name, version: req.version
            }
        }
    }
}